// Copyright 2016 Yahoo Inc.
// Licensed under the terms of the New-BSD license. Please see LICENSE file in the project root for terms.

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
//apply plugin: 'me.champeau.gradle:jmh-gradle-plugin:0.3.0'

repositories {
        jcenter()
    mavenCentral()
}


test {
  testLogging.showStandardStreams = true
  outputs.upToDateWhen {false}
  
  // enable TestNG support (default is JUnit)
  useTestNG()

  if (System.properties['os.name'] == "Mac OS X") { 
    // set a system property for the test JVM(s)
    systemProperty 'java.library.path','target/native/x86_64-darwin-clang'
  }

  if (System.properties['os.name'] == "Linux") { 
    // set a system property for the test JVM(s)
    systemProperty 'java.library.path','target/native/x86_64-linux-gcc'
  }

  testLogging {
    exceptionFormat = 'full'
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// FIXME: these bits should be a plugin we can reuse.
task buildJni(type:Exec) {
  commandLine 'make'
}

// https://docs.gradle.org/current/userguide/java_plugin.html
buildJni.shouldRunAfter compileJava

build.dependsOn(buildJni)
test.dependsOn(buildJni)

dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    testCompile group: 'org.testng', name: 'testng', version: '6.8.5'
    runtime group: 'log4j', name: 'log4j', version: '1.2.14'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.7'
    testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: 1.12
    testCompile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: 1.12
    testCompile group: 'org.openjdk.jol', name: 'jol-core', version: 0.5
    testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.16'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.yahoo.areese'
            artifactId 'javaunsafe'
            version '1.0.0'

            from components.java


            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}


bintray {
    user = bintray_user //this usually comes from gradle.properties file in ~/.gradle
    key = bintray_api_key //this usually comes from gradle.properties file in ~/.gradle
    publications = ['mavenJava'] //When uploading Maven-based publication files
    dryRun = true //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = ''
        userOrg = 'yahoo' //An optional organization name when the repo belongs to one of the user's orgs
        name = 'java-unsafe'
        desc = 'Complete madness!'
        websiteUrl = 'https://github.com/areese/JavaUnsafe'
        issueTrackerUrl = 'https://github.com/bintray/areese/JavaUnsafe/issues'
        vcsUrl = 'https://github.com/areese/JavaUnsafe.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        version {
            name = project.version //Bintray logical version name
            desc = 'optional, version-specific description'

            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = 'userToken' //OSS user token
                password = 'paasword' //OSS user password
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}

task printClasspath {
    doLast {
        configurations.testRuntime.each { print it; print ":" }
        print "build/main/classes:build/test/classes"
    }
}